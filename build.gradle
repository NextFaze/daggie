buildscript {
    apply from: 'configuration.gradle'

    repositories {
        maven {
            url 'http://maven.nextfaze.com/libs-release'
            credentials {
                username nextfazeArtifactoryUser
                password nextfazeArtifactoryPassword
            }
        }
    }

    dependencies {
        classpath rootProject.ext.kotlinPlugin
        classpath rootProject.ext.androidPlugin
        classpath rootProject.ext.androidMavenGradlePlugin
    }
}

apply from: 'configuration.gradle'

allprojects {
    repositories {
        maven {
            url 'http://maven.nextfaze.com/libs-release'
            credentials {
                username nextfazeArtifactoryUser
                password nextfazeArtifactoryPassword
            }
        }
    }

    group = GROUP
    version = VERSION_NAME
}

ext.configureLibrary = { project ->
    delegate = project

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion project.compileSdkVersion
        buildToolsVersion project.buildToolsVersion

        defaultConfig {
            minSdkVersion project.minSdkVersion

            javaCompileOptions {
                annotationProcessorOptions {
                    // We're using KAPT so ignore annotationProcessor configuration dependencies
                    includeCompileClasspath false
                }
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        lintOptions {
            abortOnError getBoolProperty('lintAbortOnError', false)
        }

        resourcePrefix "${project.name.replace("-", "_")}_"
    }

    dependencies {
        kapt project.daggerCompiler
        kaptTest project.daggerCompiler
        testImplementation project.junit
        testImplementation project.truth
        testImplementation project.mockito
        testImplementation project.mockitoKotlin
        testImplementation project.kotlinStdlib
        testImplementation project.kotlinReflect
        testImplementation project.wireMock
        testImplementation project.robolectric
    }

    apply plugin: 'com.github.dcendents.android-maven'
    apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
}

def getBoolProperty(String name, boolean defaultValue) {
    return new Boolean(hasProperty(name) ? project.properties[name].toBoolean() : defaultValue)
}
