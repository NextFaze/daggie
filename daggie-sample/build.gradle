apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'com.nextfaze.daggie.sample'
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // Dev proxy settings
        buildConfigField('String', 'DEV_PROXY_HOST', "\"${getStringProperty('androidDevProxyHost', '')}\"")
        buildConfigField('int', 'DEV_PROXY_PORT', "${getIntegerProperty('androidDevProxyPort', 0)}")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        disable 'AllowBackup', 'UnusedResources', 'GoogleAppIndexingWarning', 'InvalidPackage'
        abortOnError getBoolProperty('lintAbortOnError', false)
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries = getBoolProperty('dexPreDexLibraries', true)
    }
}

dependencies {
    implementation project(':daggie')
    implementation project(':daggie-slf4j')
    implementation project(':daggie-logback')
    implementation project(':daggie-rxjava2')
    implementation project(':daggie-manup')
    implementation project(':daggie-foreground')
    implementation project(':daggie-okhttp')
    implementation project(':daggie-threeten')
    implementation project(':daggie-jodatime')
    implementation project(':daggie-glide')
    implementation project(':daggie-gson')
    implementation project(':daggie-moshi')
    debugImplementation project(':daggie-leakcanary')
    debugImplementation project(':daggie-stetho')
    debugImplementation project(':daggie-devproxy')
    implementation rootProject.ext.supportAppCompat

    // Glide
    kapt rootProject.ext.glideCompiler

    // Dagger
    kapt rootProject.ext.daggerCompiler
    implementation rootProject.ext.dagger
    implementation rootProject.ext.daggerAnnotations

    // RxLifecycle 2
    def rxLifecycle2Version = '2.2.0'
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rxLifecycle2Version"
    implementation "com.trello.rxlifecycle2:rxlifecycle-kotlin:$rxLifecycle2Version"
    implementation "com.trello.rxlifecycle2:rxlifecycle-components:$rxLifecycle2Version"

    // Conductor
    implementation 'com.bluelinelabs:conductor:2.1.4'
    implementation 'com.bluelinelabs:conductor-rxlifecycle2:2.1.4'

    // JUnit
    testImplementation rootProject.ext.junit
}

def getStringProperty(String name, String defaultValue) {
    return hasProperty(name) ? project.properties[name].toString() : defaultValue
}

def getBoolProperty(String name, boolean defaultValue) {
    return new Boolean(hasProperty(name) ? project.properties[name].toBoolean() : defaultValue)
}

def getIntegerProperty(String name, int defaultValue) {
    return hasProperty(name) ? project.properties[name].toInteger() : defaultValue
}
